@echo off
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: 指定PORTのプロセス名を表示する。
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
set DIR=%~dp0
set NAME=%~n0
set NETSTAT=%DIR%%NAME%.netstat.txt
set FINDSTR=%DIR%%NAME%.findstr.txt
set TASKLIST=%DIR%%NAME%.tasklist.bat

set YMD=%date:~-10,4%%date:~-5,2%%date:~-2,2%
set TIME2=%time: =0%
set HMS=%TIME2:~0,2%%TIME2:~3,2%%TIME2:~6,2%

set PORT=61100
set PORT=49165
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: netstatの実行
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
echo:
netstat -aon > %NETSTAT%
if %errorlevel% neq 0 (echo netstat fail. & goto ERROR)

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: 指定PORTの抽出
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
findstr /r ":%PORT%" %NETSTAT% > %FINDSTR%
if %errorlevel% neq 0 (echo findstr %PORT% fail. &  goto ERROR)
type %FINDSTR%

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: tasklistコマンドの作成
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
echo @echo off > %TASKLIST%
:usebackqは、パス名に空白を含む場合の対応
for /F "tokens=1,2,3,4,5 usebackq" %%i in (%FINDSTR%) do (
  echo tasklist /FO TABLE /FI "PID eq %%m" >> %TASKLIST%
)

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: tasklistコマンドの実行
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
%comspec% /c %TASKLIST%
if %errorlevel% neq 0 (echo %TASKLIST% fail. & goto ERROR)

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: 終了
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:del %NETSTAT%
:del %FINDSTR%
:del %TASKLIST%

echo:
echo create %NETSTAT% %FINDSTR% %TASKLIST%.
echo:
goto NORMAL

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: エラー終了
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:ERROR
echo %YMD%-%HMS%:%NAME% fail.
exit /b 1

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: 正常終了
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:NORMAL
echo %YMD%-%HMS%:%NAME% done.
exit /b 0

